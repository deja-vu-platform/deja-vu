Cliche {
  /**
   * Capitalized ("syntactic") rules implicitly skip whitespace characters
   * Lowercase ("lexical") rules don't
   **/
  ClicheDecl = cliche name UsesDecl* "{" Paragraph* "}"

  RouteDecl = route "\""name"\""
  UsesDecl = uses name AsDecl* ("," name AsDecl*)*
  AsDecl = as name

  Paragraph = DataDecl | WidgetDecl

  DataDecl = data name ("," name)* "{" FieldBody* "}" BondDecl*

  WidgetDecl = main? widget name ("," name)* RouteDecl* WidgetUsesDecl* "{" FieldBody* "}"
  WidgetUsesDecl = uses name AsDecl* RouteDecl* ("," name AsDecl* RouteDecl*)*

  FieldBody = FieldDecl ("," FieldDecl)*
  FieldDecl = name ":" name BondDecl*

  BondDecl = "=" name (("+" | "|") name)*

  name = alnum* (("/" | "." | "_") alnum*)*
  comment = singleLineComment | multiLineComment
  singleLineComment = "//" (~eol any)*
  eol = "\r"?"\n"
  multiLineComment = "/*" (~"*/" any)* "*/"

  uses = "uses"
  widget = "widget"
  cliche = "cliche"
  data = "data"
  main = "main"
  route = "route"
  as = "as"

  // from the es5 grammar example
  space := whitespace | lineTerminator | comment
  whitespace = "\t"
             | "\x0B"    -- verticalTab
             | "\x0C"    -- formFeed
             | " "
             | "\u00A0"  -- noBreakSpace
             | "\uFEFF"  -- byteOrderMark
             | unicodeSpaceSeparator
  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"
  lineTerminator = "\n" | "\r" | "\u2028" | "\u2029"
}
