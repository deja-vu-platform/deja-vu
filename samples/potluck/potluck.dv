// Help people plan parties where guests bring supplies

// Users can create parties, invite guests, and coordinate bringing
// supplies (e.g., food, drinks). Guests can create supply requests
// (e.g., "2 cakes") and can commit to bringing requests.
app Potluck {
  include access/StandardAuthentication[User for User]
  include access/Authorization[User for User]

  // Create parties
  include organization/Event[Party for Event]

  // Invite guests to parties
  include organization/Group[
    User for Member,
    Party for Group,
    ShowParty replacing ShowNameOfGroup in ShowGroupsByMember
  ]

  // Coordinate bringing supplies
  include community/Market[
    /** Entity bindings **/
    // Each party is a market in which party guests trade supply requests
    Party for Market, User for Party,
    // A guest can create a supply request (create a new good she's selling) and
    // other guests can commit to bringing the supply (buy goods)
    SupplyRequest for Good, Commitment for Transaction,

    /** Widget replacements **/
    ShowSupplyRequest replacing ShowGood in ShowAllGoods,
    ShowCommitment replacing ShowTransaction in ShowAllTransactions
  ]

  /* Entities */
  data User {
    username: text
  }

  data Party {}

  // A request to bring a certain amount of items to the party
  data SupplyRequest {
    name: text,
    party: Party,
    quantity: number, // the amount of items needed
    user: User  // the user that created the supply request
  }

  // A commitment done by a user to bring a certain amount of items to a party
  data Commitment {
    supply: SupplyRequest,
    provider: User,
    party: Party,
    // this quantity could be less than the total amount of items requested
    quantity: number 
  }

  /* Widgets */
  // The landing page gives the user the option to sign in or register
  // After a new user is created the user is also automatically signed in
  main widget Landing {
    // Redirect the user to the Parties widget after a successful sign in 
    on_user_ok: Widget = Parties
  }

  // The main home page. Lists all the parties the user is invited to
  widget Parties {
    user: User
  }

  // Shows the Potluck logo and a sign out button if the user is logged in
  widget NavBar {
    user: User
  }

  widget NewParty {
    party: Party,
    user: User,
    submit_ok: boolean
  }

  widget ShowParty {
    party: Party,
    user: User
  }

  // used in ShowParty
  // Shows all commitments and supply requests and lets the user commit to
  // bring supplies
  widget ShowSupplies {
    party: Party
  }

  // used in ShowParty
  // Creates a new supply request
  widget RequestSupply {
    party: Party,
    // the user making the request
    user: User,
    // the request supply being created
    new_supply_request: SupplyRequest
  }

  // Show a supply request and lets the logged in user commit to bringing some
  // (or all) of the items requested
  widget ShowSupplyRequest {
    supply_request: SupplyRequest,

    logged_in_user: User,
    quantity: number
  }

  widget ShowCommitment {
    commitment: Commitment
  }


  /* Routes */
  parties_route: route = {path: "parties", widget: Parties}
  new_party_route: route = {path: "new-party", widget: NewParty}
}
