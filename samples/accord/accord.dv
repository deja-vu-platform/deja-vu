app Accord {

  include access/Auth[User for User]

  include organization/Group[
    User for Member,
    SongGroup for Group,
    LinkToGroup replacing ShowGroupOverview in ShowGroupsByMember
  ]

  include organization/List[
    Setlist for Item,
    SongGroup for List,
    LinkToSetlist replacing ShowItem in ShowList
  ] as SetlistList

  include organization/List[
    Song for Item,
    Setlist for List,
    ShowSong replacing ShowItem in ShowList
  ] as SongList

  include community/Rating[
    User for Source,
    Song for Target
  ]

  data User {
    username: text
  }

  data SongGroup {
    name: text,
    members: [User],
    items: [Setlist]
  }

  data Song {
    name: text
  }

  data Setlist {
    name: text,
    songs: [Song]
  }

  data SetlistCollection {
    name: text,
    members: [Setlist]
  }

  // The landing page gives the user the option to sign in or register
  main widget Landing {
    user_ok_redirect_route: text
  }

  // The about page lets the user see an explanation of the project
  widget About {
  }

  // The Register page lets the user create a new account
  widget Register {
    user_ok_redirect_route: text
  }

  // Allows a user to view existing groups and go to the CreateGroup page
  widget Groups {
    user: User
  }

  // Create a new group
  widget CreateGroup {
    initialMember: User,
    groupCreatedRedirectRoute: text
  }

  // Add a member to the group
  widget AddMember {
    group: SongGroup
  }

  // View the information about a group
  widget ViewGroup {
    group: SongGroup,
    prefix: text
  }

  // View a setlist
  widget ViewSetlist {
    list: Setlist,
    prefix: text
  }

  // Show a song on the view setlist page
  widget ShowSong {
    item: Song
  }

  // Links to a group on the Groups view page
  widget LinkToGroup {
    group: SongGroup,
    prefix: text
  }

  // Links to a setlist on the Group view page
  widget LinkToSetlist {
    setlist: Setlist,
    prefix: text
  }

  // Create a new setlist
  widget CreateSetlist {
    list: SongGroup
  }

  // Create a new proposal
  widget CreateProposal {
    list: Setlist
  }

  create_group_page: CreateGroup = {groupCreatedRedirectRoute: "/groups"}
  ltg: LinkToGroup = {prefix: "/groups"}
  lts: LinkToSetlist = {prefix: "/groups/setlists"}
  view_group_page: ViewGroup = {prefix: "/groups/create-setlist"}
  view_setlist_page: ViewSetlist = {prefix: "/groups/setlists/create-proposal/"}

  john: User = {username: "john"}
  // g1: Group = {name:"John's Group", members: [u1]}

  l: Landing = {user_ok_redirect_route: "/groups"}
  reg: Register = {user_ok_redirect_route: "/groups"}

  landing_route: Route = {path: "landing", widget: Landing}
  about: Route = {path: "about", widget: About}
  register: Route = {path: "register", widget: Register}
  groups: Route = {path: "groups", widget: Groups}
  create_group: Route = {path: "groups/create", widget: CreateGroup}
  add_member: Route = {path: "groups/add-member/:group_id", widget: AddMember}
  view_group: Route = {path: "groups/:group_id", widget: ViewGroup}
  create_setlists: Route = {path: "groups/create-setlist/:group_id", widget: CreateSetlist}
  view_setlist: Route = {path: "groups/setlists/:list_id", widget: ViewSetlist}
  create_proposal: Route = {path: "groups/setlists/create-proposal/:list_id", widget: CreateProposal}
}
