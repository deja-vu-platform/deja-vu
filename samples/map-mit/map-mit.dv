// Allows members of the MIT community to plan events on campus
app MapMit {

  // Restrict access to the application to only registered users
  include access/StandardAuthentication[User for User]
  include access/Authorization[User for Principal]

  // One map shows the locations of events
  include community/Geolocation[
    CampusEvent for Marker,
    CampusMap for Map,
    EventInfoWindow replacing MarkerInfoWindow in OverlayMarker
  ]

  // Another map allows the user to search for places to set event location
  include community/Geolocation[
    CampusEvent for Marker,
    CampusMap for Map
  ] as SearchGeolocation

  include organization/Event[
    CampusEvent for Event,
    DisplayEvent replacing ShowEvent in ShowEvents
  ]

  include organization/Group[
    User for Member,
    GroupData for Group,
    DisplayGroup replacing ShowNameOfGroup in ShowGroupsByMember
  ]

  data User {
    username: text
  }

  data CampusMap {}

  data CampusEvent {
    name: text
  }

  data GroupData {}

  events_map: CampusMap = {}

  search_map: CampusMap = {}

  main widget Login {
    on_signin_ok: Widget = Home
  }

  widget Signup {
    on_register_ok: Widget = Home
  }

  widget NavBar {
    user: User
  }

  widget Home {
    map: CampusMap = events_map
  }

  widget MyEvents {}

  widget CreateEvent {
    map: CampusMap = search_map,
    event: CampusEvent,
    submit_ok: boolean
  }

  widget DisplayEvent {
    map: CampusMap = events_map,
    event: CampusEvent
  }

  widget DisplayGroup {
    group: GroupData
  }

  widget MyGroups {
    user: User,
    group: GroupData,
    submit_ok: boolean
  }

  widget EventInfoWindow {
    event: CampusEvent
  }

  login: route = {path: "login", widget: Login}
  signup: route = {path: "signup", widget: Signup}
  home: route = {path: "home", widget: Home}
  my_events: route = {path: "my-events", widget: MyEvents}
  create_event: route = {path: "create-event", widget: CreateEvent}
  my_groups: route = {path: "my-groups", widget: MyGroups}
}
