app SweetSpots {
  include access/Auth[
    User for User
  ]
  include community/Location[
    Spot for Marker,
    MapData for Map,
    SpotInfoWindow replacing MarkerInfoWindow in OverlayMarker
  ]
  include community/Rating[
    User for Source,
    Spot for Target
  ]
  include messaging/Comment[
    User for Author,
    Spot for Target,
    CommentData for Comment,
    Review replacing ShowComment in ShowComments
  ]
  include organization/Label[
    Spot for Item,
    SpotArr for ItemArr,
    SpotPanelWithMarker replacing ShowItem in ShowSearchResults
  ]
  include community/Follow[
    User for Source,
    Spot for Target,
    SpotPanel replacing ShowTarget in ShowFollowing
  ]

  data User {
    username: text
  }

  data Spot {
    name: text
  }

  // TODO: Change once array fields work
  data SpotArr {
    arr: [Spot]
  }

  data MapData {}

  global_map: MapData

  data CommentData {}

  main widget Main {
    map: MapData = global_map,
    new_spot: Spot,
    user: User,
    search: boolean,
    search_results: SpotArr
  }

  widget NavBar {
    user: User,
    search_results: SpotArr
  }

  widget NavBarWithoutSearch {
    user: User,
    search_results: SpotArr,
    to_profile: Widget = Profile,
    to_login: Widget = Login,
    to_register: Widget = Register
  }

  widget Login {
    on_login_ok: Widget = Main
  }

  widget Register {
    on_register_ok: Widget = Main
  }

  widget AddASpot {
    map: MapData,
    new_spot: Spot
  }

  widget AddASpotForm {
    map: MapData,
    new_spot: Spot,
    user: User,
    submit_ok: boolean
  }

  widget SpotInfoWindow {
    spot: Spot,
    to_spot_details: Widget = SpotDetails
  }

  widget SpotDetails {
    map: MapData = global_map,
    spot: Spot,
    user: User
  }

  widget Profile {
    user: User
  }

  widget Review {
    comment: CommentData,
    spot: Spot
  }

  widget SpotPanel {
    spot: Spot,
    to_spot_details: Widget = SpotDetails
  }

  widget SpotPanelWithMarker {
    map: MapData = global_map,
    spot: Spot,
    to_spot_details: Widget = SpotDetails
  }

  login: route = {path: "login", widget: Login}
  register: route = {path: "register", widget: Register}
  spot_details: route = {path: "spots", widget: SpotDetails}
  profile: route = {path: "profile", widget: Profile}
}
