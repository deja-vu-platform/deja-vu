app SweetSpots {
  include access/Auth[User for User]
  include map/Map[
    Spot for Marker,
    MapData for Map,
    SpotInfoWindow replacing MarkerInfoWindow in OverlayMarker
  ]
  include community/Rating[User for Source, Spot for Target]
  include messaging/Comment[User for Author, Spot for Target]
  include organization/Label[Spot for Item, SpotArr for ItemArr]

  data User {
    username: text
  }

  data Spot {
    name: text
  }

  // TODO: Change once array fields work
  data SpotArr {
    arr: [Spot]
  }

  data MapData {}

  global_map: MapData

  main widget Main {
    map: MapData = global_map,
    new_spot: Spot,
    user: User,
    search: boolean,
    search_results: SpotArr
  }

  widget NavBar {
    user: User,
    search_results: SpotArr
  }

  widget Login {
    on_login_ok: Widget = Main
  }

  widget Register {
    on_register_ok: Widget = Main
  }

  widget AddASpot {
    map: MapData,
    new_spot: Spot
  }

  widget AddASpotForm {
    map: MapData,
    new_spot: Spot,
    user: User,
    submit_ok: boolean
  }

  widget SpotInfoWindow {
    spot: Spot,
    to_spot_details: Widget = SpotDetails
  }

  widget SpotDetails {
    spot: Spot,
    user: User
  }

  widget Profile {
    user: User
  }

  login: route = {path: "login", widget: Login}
  register: route = {path: "register", widget: Register}
  spot_details: route = {path: "spots", widget: SpotDetails}
  profile: route = {path: "profile", widget: Profile}
}
