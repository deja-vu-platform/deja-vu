// Bookmark is an app for users to post updates with topics and follow other
// users or topics. Users get a feed with all the relevant updates given the
// topics and users they follow.
cliche Bookmark uses
  community/Follow as FollowUsers,
  community/Follow as FollowTopics, 
  access/Auth, messaging/Feed,
  organization/Label,
  messaging/Post as MessagingPost {

  /**
   *  Data definitions
   */
  data User {
    username: Text =
      FollowUsers.Source.name + FollowUsers.Target.name +
      FollowTopics.Source.name + Auth.User.username +
      MessagingPost.User.username +
      Feed.Subscriber.name + Feed.Publisher.name,
    posts: Post = Feed.Publisher.published + MessagingPost.User.posts,
    follows: User = FollowUsers.Source.follows + Feed.Subscriber.subscriptions |
      FollowTopics.Source.follows + Feed.Subscriber.subscriptions
  } = // users can follow other users
      FollowUsers.Source + FollowUsers.Target +
      // users can follow topics
      FollowTopics.Source +
      MessagingPost.User + Feed.Subscriber + Feed.Publisher
  
  data Post {
    content: Text = MessagingPost.Post.content + Feed.Message.content,
    topics: Topic = Label.Item.labels
  } = Label.Item + MessagingPost.Post + Feed.Message
  
  data Topic {
    name: Text = Label.Item.name + Feed.Publisher.name +
        FollowTopics.Target.name
  } = Label.Label + FollowTopics.Target + Feed.Publisher

  /**
   * Widgets
   */
  // The landing page gives the user the option to sign in or register
  main widget Landing route "landing" uses
    Auth.SignInWithRedirect, Auth.RegisterWithRedirect {
    user_ok_redirect: App = Auth.RegisterWithRedirect.register_ok_redirect +
      Auth.SignInWithRedirect.signin_ok_redirect
  }

  widget App route "app" uses
    Auth.SignOutWithRedirect,
    Home route "home",
    Users route "users",
    Topics route "topics" {
    sign_out_ok_redirect: Landing = Auth.SignOutWithRedirect.signout_ok_redirect
  }

  widget Home uses Auth.LoggedIn, CreatePost, Feed.ShowFeed {
    user: User = Auth.LoggedIn.user + CreatePost.author +
      Feed.ShowFeed.subscriber
  }

  // Shows the post, with its author, attached labels and comments. Also has
  // a field that lets the logged in user comment on the post
  widget ShowFeedPost uses Author, Labels, CommentsWithComment {
    post: Post
  } replaces Feed.ShowFeedItem { message = post }

  widget CreatePost uses
    MessagingPost.NewPostContent,
    Label.LabelsText,
    MessagingPost.NewPostButton {
    post: Post = MessagingPost.NewPostContent.post + Label.LabelsText.item +
      MessagingPost.NewPostButton.post,
    submit_ok: Boolean = MessagingPost.NewPostContent.submit_ok +
      Label.LabelsText.submit_ok + MessagingPost.NewPostButton.submit_ok
  }

  // Displays the list of topics and lets the logged in user follow or unfollow
  // them
  widget Topics uses Auth.LoggedIn, FollowTopics.EditFollow {
    user: User = Auth.LoggedIn.user + FollowTopics.EditFollow.source
  }

  // Displays the list of users and lets the logged in user follow or unfollow
  // them
  widget Users uses LoggedIn, FollowUsers.EditFollow {
    user: User = Auth.LoggedIn.user + FollowUsers.EditFollow.source
  }
}
