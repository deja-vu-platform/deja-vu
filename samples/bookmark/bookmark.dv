// Bookmark is an app for users to post updates with topics and follow other
// users or topics. Users get a feed with all the relevant updates given the
// topics and users they follow.
app Bookmark {

  include community/Follow[User for Source, User for Target] as FollowUsers
  include community/Follow[User for Source, Topic for Target] as FollowTopics
  include access/Auth[User for User, Post for Resource]
  include messaging/Feed[
    User for Subscriber, User | Topic for Publisher, Post for Message,
    ShowFeedPost replacing ShowFeedItem in ShowFeed
  ]
  include organization/Label[Post for Item, Topic for Label]
  include messaging/Post[User for User, Post for Post] as MessagingPost
  include messaging/Comment[User for Author, Post for Target]

  /**
   *  Data definitions
   */
  data User {
    username: Text,
    posts: Post,
    follows: User | Topic
  }

  u1: User = {username: "benbitdiddle", posts: [p1]}
  u2: User = {username: "alyssaphacker", posts: [p2]}

  data Post {
    content: Text,
    topics: Topic,
    author: User
  }

  p1: Post = {content: "hello, I'm Ben", author: u1, topics: [t1]}
  p2: Post = {content: "hello, I'm Alyssa", author: u2, topics: [t1]}

  data Topic {
    name: Text,
    posts: Post
  }

  t1: Topic = {name: "hello", posts: [p1, p2]}

  /**
   * Widgets
   */
  // The landing page gives the user the option to sign in or register
  main widget Landing {
    user_ok_redirect_route: Text
  }

  l: Landing = {user_ok_redirect_route: "/app/home"}


  widget App {
    signout_ok_redirect_route: Text
  }

  a: App = {signout_ok_redirect_route: "/landing"}

  widget Home {
    user: User
  }

  // Shows the post, with its author, attached labels and comments. Also has
  // a field that lets the logged in user comment on the post
  widget ShowFeedPost {
    feed_user: User,
    post: Post
  }

  widget CreatePost {
    author: User,
    post: Post,
    submit_ok: Boolean
  }

  // Displays the list of topics and lets the logged in user follow or unfollow
  // them
  widget Topics {
    user: User
  }

  // Displays the list of users and lets the logged in user follow or unfollow
  // them
  widget Users {
    user: User
  }

  landing_route: Route = {path: "landing", widget: Landing}
  app_route: Route = {
    path: "app", widget: App,
    children: [
      {path: "home", widget: Home},
      {path: "users", widget: Users},
      {path: "topics", widget: Topics}
    ]
  }
}
