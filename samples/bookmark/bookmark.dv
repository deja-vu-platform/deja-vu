// Bookmark is an app for users to post updates with topics and follow other
// users or topics. Users get a feed with all the relevant updates given the
// topics and users they follow.
app Bookmark {

  use community/Follow[User: Source, User: Target] as FollowUsers
  use community/Follow[User: Source, Topic: Target] as FollowTopics
  use access/Auth[User: User, Post: Resource]
  use messaging/Feed[
    User: Subscriber, User | Topic: Publisher, Post: Message,
    ShowFeedPost replaces ShowFeedItem in ShowFeed
  ]
  use organization/Label[Post: Item, Topic: Label]
  use messaging/Post[User: User, Post: Post] as MessagingPost
  use messaging/Comment[User: Author, Post: Target]

  /**
   *  Data definitions
   */
  data User {
    username: Text,
    posts: Post,
    follows: User | Topic
  }

  u1: User = {username: "benbitdiddle", posts: [p1]}
  u2: User = {username: "alyssaphacker", posts: [p2]}

  data Post {
    content: Text,
    topics: Topic,
    author: User
  }

  p1: Post = {content: "hello, I'm Ben", author: u1, topics: [t1]}
  p2: Post = {content: "hello, I'm Alyssa", author: u2, topics: [t1]}

  data Topic {
    name: Text,
    posts: Post
  }

  t1: Topic = {name: "hello", posts: [p1, p2]}

  /**
   * Widgets
   */
  // The landing page gives the user the option to sign in or register
  main widget Landing {
    on_user_ok: Widget
  }

  l: Landing = {on_user_ok: Home}


  widget NavBar {
    on_signout_ok: Widget
  }

  nb: NavBar = {on_signout_ok: Landing}

  widget Home {
    user: User
  }

  // Shows the post, with its author, attached labels and comments. Also has
  // a field that lets the logged in user comment on the post
  widget ShowFeedPost {
    feed_user: User,
    post: Post
  }

  widget CreatePost {
    author: User,
    post: Post,
    submit_ok: Boolean
  }

  // Displays the list of topics and lets the logged in user follow or unfollow
  // them
  widget Topics {
    user: User
  }

  // Displays the list of users and lets the logged in user follow or unfollow
  // them
  widget Users {
    user: User
  }

  landing_route: Route = {path: "landing", widget: Landing}
  home_route: Route = {path: "home", widget: Home}
  users_route: Route = {path: "users", widget: Users}
  topics_route: Route = {path: "topics", widget: Topics}
}
